apply plugin: 'com.android.application'

ext.versionMajor = 0
ext.versionMinor = 9
ext.versionPatch = 9
ext.versionClassifier = "beta" // or null
ext.isSnapshot = true // set to false when publishing new releases
ext.minimumSdkVersion = 21
ext.targetSdkVersion = 29

private Integer GenerateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String GenerateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    if (ext.isSnapshot) {
        versionName += "-" + "SNAPSHOT"
    }
    return versionName;
}

android {
    signingConfigs {
        release {
            storeFile file('C:\\Users\\LiuYujie\\keystore.jks')
            storePassword 'mt123456'
            keyPassword 'mt123456'
            keyAlias 'key0'
        }
    }
    ndkVersion "21.3.6528147"
    compileSdkVersion project.ext.targetSdkVersion

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "io.github.trojan_gfw.igniter"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode GenerateVersionCode()
        versionName GenerateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        /*ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }*/
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_CPP_FEATURES=rtti exceptions"
            }
        }
        archivesBaseName = "$applicationId-v$versionName-$versionCode"
        signingConfig signingConfigs.release
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "arm64-v8a", "armeabi-v7a"
            universalApk false
        }
    }
}

repositories {
    flatDir {
        dirs 'src/libs'
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta8'
    implementation 'androidx.core:core:1.5.0-alpha01'
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.7'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-rc01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc01'

    api(name: 'golibs', ext: 'aar')
}
